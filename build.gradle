buildscript {
    ext {
        springBootVersion = '1.1.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
}

subprojects {

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }


    dependencies {
        compile("org.springframework.cloud:spring-cloud-spring-service-connector")
        compile("org.springframework.cloud:spring-cloud-cloudfoundry-connector")
        compile("org.springframework.boot:spring-boot-starter-integration")
        compile("org.springframework.boot:spring-boot-starter-amqp")
        compile("org.springframework.integration:spring-integration-amqp")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-actuator")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        runtime("org.hsqldb:hsqldb")
    }

}

project(':springbox-kiosk') {

    apply plugin: 'spring-boot'

    jar {
        baseName = 'springbox-kiosk'
        version = '0.0.1-SNAPSHOT'
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("org.springframework.boot:spring-boot-starter-data-rest")

        versionManagement 'io.spring.platform:platform-versions:1.0.2.RELEASE@properties'
    }
}

project(':springbox-inventory') {
    apply plugin: 'spring-boot'

    jar {
        baseName = 'springbox-inventory'
        version = "0.0.1-SNAPSHOT"
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-redis")

        versionManagement 'io.spring.platform:platform-versions:1.0.2.RELEASE@properties'
    }
}

project(':springbox-catalog') {
    apply plugin: 'spring-boot'

    jar {
        baseName = 'springbox-catalog'
        version = "0.0.1-SNAPSHOT"
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-data-mongodb")
        compile("org.springframework.boot:spring-boot-starter-data-rest")

        versionManagement 'io.spring.platform:platform-versions:1.0.2.RELEASE@properties'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
